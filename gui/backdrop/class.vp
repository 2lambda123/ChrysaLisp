(include 'sys/func.inc)
(include 'gui/ctx/class.inc)
(include 'gui/backdrop/class.inc)
(include 'class/sym/class.inc)

(gen-vtable 'backdrop)
(gen-create 'backdrop)

(def-method 'backdrop 'draw)
	;inputs
	;r0 = view object (ptr)
	;r1 = draw ctx (ptr)
	;outputs
	;r0 = view object (ptr)
	;trashes
	;r1-r14

	(ptr 'this 'ctx)
	(int 'x 'y 'w 'h)
	(uint 'col 'ink)

	(push-scope)
	(entry 'backdrop 'draw {this, ctx})

	(call 'backdrop 'get_long_prop {this, static_sym_color} {_, col})
	(call 'backdrop 'get_long_prop {this, static_sym_ink_color} {_, ink})
	(call 'backdrop 'get_bounds {this} {_, _, _, w, h})

	(call 'ctx 'set_color {ctx, col})
	(call 'ctx 'filled_box {ctx, 0, 0, w, h})
	(assign {0 => x} {y})
	(call 'ctx 'set_color {ctx, ink})
	(loop-while {x < w})
		(call 'ctx 'filled_box {ctx, x, 0, 1, h})
		(assign {x + 32} {x})
	(loop-end)
	(loop-while {y < h})
		(call 'ctx 'filled_box {ctx, 0, y, w, 1})
		(assign {y + 32} {y})
	(loop-end)

	(exit 'backdrop 'draw {this})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'backdrop 'layout)
	;inputs
	;r0 = backdrop object (ptr)
	;outputs
	;r0 = backdrop object (ptr)
	;trashes
	;r1-r14

	(entry 'backdrop 'layout '(r0))

	(call 'backdrop 'get_long_prop '(r0 static_sym_color) '(r0 r1))
	(vp-shr-cr 24 r1)
	(vpif '(r1 = 0xff))
		(jump 'view 'set_flags '(r0 view_flag_opaque view_flag_opaque) '(r0 _))
	(endif)

	(exit 'backdrop 'layout '(r0))
	(vp-ret)

(def-func-end)

(def-method 'backdrop 'pref_size)
	;inputs
	;r0 = backdrop object (ptr)
	;outputs
	;r0 = backdrop object (ptr)
	;r9 = preferred width (pixels)
	;r10 = preferred height (pixels)
	;trashes
	;r9-r10

	(entry 'backdrop 'pref_size '(r0))
	(exit 'backdrop 'pref_size '(r0 640 480))
	(vp-ret)

(def-func-end)

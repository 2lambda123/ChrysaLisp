(include 'sys/func.inc)
(include 'gui/canvas/class.inc)
(include 'gui/ctx/class.inc)
(include 'class/stream/class.inc)
(include 'class/array/class.inc)

(def-struct 'cpm)
	(uint 'ident)		;ident '.CPM' or '.FLM'
	(uint 'bytes)		;0 if not known, else total size
	(uint 'version)		;version number
	(uint 'type)		;image type
	(uint 'width)		;image w
	(uint 'height)		;image h
	(uint 'trans)		;image transparent colour
(def-struct-end)

(def-method 'canvas 'load_cpm)
	;inputs
	;r0 = stream object (ptr)
	;outputs
	;r0 = 0 if error, else canvas object (ptr)
	;trashes
	;all

	(ptr 'this 'stream)
	(puint 'data 'data_end)
	(ulong 'bit_pool)
	(struct 'header 'cpm)
	(int 'length 'bit_pool_size)
	(ulong 'col)

	;save inputs
	(push-scope)
	(entry 'canvas 'load_cpm {stream})

	;read header
	(call 'stream 'read {stream, &header, cpm_size} {_, length})
	(gotoif {length != cpm_size} 'error1)

	;check ident '.CPM' or '.FLM'
	(gotoifnot (cat {header.cpm_ident == } (str (code "MPC." 4))
			{ || header.cpm_ident == } (str (code "MLF." 4))) 'error1)

	;create canvas
	(call 'canvas 'create {header.cpm_width, header.cpm_height, 1} {this})
	(vpif {this})
		;fill with transparent
		(call 'canvas 'fill {this, 0})
		(assign {header.cpm_type} {this->canvas_stream_type})
		(assign {this->canvas_array->array_array} {data})
		(assign {data + this->canvas_height * this->canvas_stride} {data_end})
		(assign {0, 0} {bit_pool, bit_pool_size})

		;decode acording to type
		(vp-def (col len trans data data_end))
		(loop-start)
			;get token
			(call 'stream 'read_bits {stream, 8, bit_pool, bit_pool_size} {_, length, bit_pool, bit_pool_size})
			(assign {length} (list len))
			(gotoif `(,len == -1) 'error)
			(vpif `(,len >= 128))
				;run of a pixel
				(vp-sub-cr 127 len)
				(assign (list len) {length})
				(call 'canvas 'read_pixel {stream, header.cpm_type, bit_pool, bit_pool_size} {col, bit_pool, bit_pool_size})
				(assign {col} (list col))
				(gotoif `(,col == -1) 'error)

				;fill or skip
				(assign {length, header.cpm_trans, data, data_end}
					(list len trans data data_end))
				(vpif `(,col != ,trans))
					(loop-start)
						(gotoif `(,data >= ,data_end) 'error)
						(vp-cpy-ri-i col data 0)
						(vp-sub-cr 1 len)
						(vp-add-cr int_size data)
					(loop-until `(,len == 0))
				(else)
					(vp-shl-cr (log2 int_size) len)
					(vp-add-rr len data)
				(endif)
			(else)
				;block of pixels
				(vp-add-cr 1 len)
				(loop-start)
					(assign (list len) {length})
					(call 'canvas 'read_pixel {stream, header.cpm_type, bit_pool, bit_pool_size} {col, bit_pool, bit_pool_size})
					(assign {col} (list col))
					(gotoif `(,col == -1) 'error)
					(assign {length, header.cpm_trans, data, data_end}
						(list len trans data data_end))
					(gotoif `(,data >= ,data_end) 'error)
					(vpif `(,col != ,trans))
						(vp-cpy-ri-i col data 0)
					(endif)
					(vp-sub-cr 1 len)
					(vp-add-cr int_size data)
					(assign (list data) {data})
				(loop-until `(,len == 0))
			(endif)
			(assign (list data) {data})
		(loop-until `(,data == ,data_end))
		(call 'canvas 'as_premul {this, this})
	(endif)

	(exit 'canvas 'load_cpm {this})
	(return)

(vp-label 'error)
	(call 'canvas 'deref {this})
(vp-label 'error1)
	(exit 'canvas 'load_cpm '(0))
	(pop-scope)
	(return)

(def-func-end)

(def-method 'canvas 'next_frame)
	;inputs
	;r0 = canvas object (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;all but r0

	(ptr 'this)
	(puint 'data 'data_end)
	(int 'length)
	(ulong 'col)

	;save inputs
	(push-scope)
	(entry 'canvas 'next_frame {this})

	(vpif {this->canvas_stream} 'error)
		;decode acording to type
		(assign {this->canvas_array->array_array} {data})
		(assign {data + this->canvas_height * this->canvas_stride} {data_end})
		(vp-def (col len data data_end))
		(loop-start)
			;get token, loop back if stream end
			(loop-start)
				(call 'stream 'read_char {this->canvas_stream} (list '_ len))
				(breakif `(,len != -1))
				(assign {this->canvas_bufp} {this->canvas_stream->stream_bufp})
			(loop-end)

			;decode token
			(vpif `(,len >= 128))
				;skip
				(assign {data, data_end} (list data data_end))
				(vp-sub-cr 256 len)
				(vp-shl-cr (log2 int_size) len)
				(vp-sub-rr len data)
			(else)
				;block of pixels
				(loop-start)
					(assign (list len) {length})
					(call 'canvas 'read_pixel {this->canvas_stream, this->canvas_stream_type, 0, 0} {col, _, _})
					(assign {col} (list col))
					(breakif `(,col == -1) 'error)
					(assign {length, data, data_end} (list len data data_end))
					(breakif `(,data >= ,data_end) 'error)
					(vp-cpy-ri-i col data 0)
					(vp-sub-cr 1 len)
					(vp-add-cr int_size data)
					(assign (list data) {data})
				(loop-until `(,len == 0))
			(endif)
			(assign (list data) {data})
		(loop-until `(,data == ,data_end))
	(endif)

	(exit 'canvas 'next_frame {this})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'canvas 'read_pixel)
	;inputs
	;r0 = stream object (ptr)
	;r1 = pixel type (uint)
	;r2 = bit pool (ulong)
	;r3 = bit pool size (uint)
	;outputs
	;r0 = -1 if error, else pixel (long)
	;r1 = bit pool (ulong)
	;r2 = bit pool size (uint)
	;trashes
	;all

	(entry 'canvas 'read_pixel '(r0 r1 r2 r3))

	(vp-push r1)
	(call 'stream 'read_bits '(r0 r1 r2 r3) '(_ r0 r1 r2))
	(vp-pop r3)
	(vp-push r1 r2)
	(call 'canvas 'to_argb32 '(r0 r3) '(r0))
	(vp-pop r1 r2)

	(exit 'canvas 'read_pixel '(r0 r1 r2))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'to_argb32)
	;inputs
	;r0 = col (uint)
	;r1 = pixel type (uint)
	;outputs
	;r0 = col (uint)
	;trashes
	;r1-r7

	(vp-def (col pix rh rl gh gl bh bl) '(r0 r1))

	(entry 'canvas 'to_argb32 (list col pix))

	(switch)
	(case `(,pix == 32))
		(vp-cpy-cr argb_black pix)
		(vp-xor-rr pix col)
		(break)
	(case `(,pix == 24))
		(vp-cpy-cr argb_black pix)
		(vp-add-rr pix col)
		(break)
	(case `(,pix == 16))
		(vp-cpy-rr col rh)
		(vp-cpy-rr col rl)
		(vp-cpy-rr col gh)
		(vp-cpy-rr col gl)
		(vp-cpy-rr col bh)
		(vp-cpy-rr col bl)
		(vp-and-cr 0b1111100000000000 rh)
		(vp-and-cr 0b1110000000000000 rl)
		(vp-and-cr 0b11111100000 gh)
		(vp-and-cr 0b11000000000 gl)
		(vp-and-cr 0b11111 bh)
		(vp-and-cr 0b11100 bl)
		(vp-shl-cr 8 rh)
		(vp-shl-cr 3 rl)
		(vp-shl-cr 5 gh)
		(vp-shr-cr 1 gl)
		(vp-shl-cr 3 bh)
		(vp-shr-cr 2 bl)
		(vp-cpy-cr argb_black col)
		(vp-add-rr rh rl)
		(vp-add-rr gh gl)
		(vp-add-rr bh bl)
		(vp-add-rr rl col)
		(vp-add-rr gl col)
		(vp-add-rr bl col)
		(break)
	(default)
		(vpif '(col == 0))
			(vp-cpy-cr argb_black col)
		(else)
			(vp-cpy-cr argb_white col)
		(endif)
	(endswitch)

	(exit 'canvas 'to_argb32 (list col))
	(vp-ret)

(def-func-end)

(include 'sys/func.inc)
(include 'gui/canvas/class.inc)
(include 'gui/ctx/class.inc)
(include 'class/stream/class.inc)
(include 'class/array/class.inc)

(def-struct 'cpm)
	(uint 'ident)		;ident '.CPM' or '.FLM'
	(uint 'bytes)		;0 if not known, else total size
	(uint 'version)		;version number
	(uint 'type)		;image type
	(uint 'width)		;image w
	(uint 'height)		;image h
	(uint 'trans)		;image transparent colour
(def-struct-end)

(def-method 'canvas 'load_cpm)
	;inputs
	;r0 = stream object (ptr)
	;outputs
	;r0 = 0 if error, else canvas object (ptr)
	;trashes
	;all

	(ptr 'this 'stream)
	(puint 'data 'data_end)
	(struct 'header 'cpm)
	(int 'length)

	;save inputs
	(push-scope)
	(entry 'canvas 'load_cpm {stream})

	;read header
	(assign {0} {this})
	(call 'stream 'read {stream, &header, cpm_size} {_, length})
	(vpif {length == cpm_size})
		;check ident '.CPM' or '.FLM'
		(vpif (cat {header.cpm_ident == } (str (code "MPC." 4))
				{ || header.cpm_ident == } (str (code "MLF." 4))))
			;create canvas
			(call 'canvas 'create {header.cpm_width, header.cpm_height, 1} {this})
			(vpif {this})
				;fill with transparent
				(call 'canvas 'fill {this, 0})
				(assign {this->canvas_array->array_array} {data})
				(assign {data + this->canvas_height * this->canvas_stride} {data_end})

				;decode acording to version
				(vp-def (col len trans data data_end))
				(loop-start)
					;get token
					(call 'stream 'read_char {stream} {_, length})
					(gotoif {length == -1} 'error)
					(vpif {length >= 128})
						;run of a pixel
						(call 'canvas 'read_pixel {stream, header.cpm_type} (list col))
						(gotoif `(,col == -1) 'error)

						;fill or skip
						(assign {length - 127, header.cpm_trans, data, data_end}
							(list len trans data data_end))
						(vpif `(,col != ,trans))
							(loop-start)
								(gotoif `(,data >= ,data_end) 'error)
								(vp-cpy-ri-i col data 0)
								(vp-sub-cr 1 len)
								(vp-add-cr int_size data)
							(loop-until `(,len == 0))
						(else)
							(vp-shl-cr (log2 int_size) len)
							(vp-add-rr len data)
						(endif)
					(else)
						;block of pixels
						(assign {length + 1} (list len))
						(loop-start)
							(assign (list len) {length})
							(call 'canvas 'read_pixel {stream, header.cpm_type} (list col))
							(gotoif `(,col == -1) 'error)
							(assign {length, header.cpm_trans, data, data_end}
								(list len trans data data_end))
							(gotoif `(,data >= ,data_end) 'error)
							(vpif `(,col != ,trans))
								(vp-cpy-ri-i col data 0)
							(endif)
							(vp-sub-cr 1 len)
							(vp-add-cr int_size data)
							(assign (list data) {data})
						(loop-until `(,len == 0))
					(endif)
					(assign (list data) {data})
				(loop-until `(,data == ,data_end))
			(endif)
		(endif)
	(endif)
	(call 'canvas 'as_premul {this, this})

(vp-label 'funcexit)
	(exit 'canvas 'load_cpm {this})
	(return)

(vp-label 'error)
	(call 'canvas 'deref {this})
	(assign {0} {this})
	(goto 'funcexit)

	(pop-scope-syms)

(def-func-end)

(def-method 'canvas 'read_pixel)
	;inputs
	;r0 = stream object (ptr)
	;r1 = pixel type (uint)
	;outputs
	;r0 = -1 if error, else pixel (long)

	(vp-def (col pix stream) '(r0 r1))

	(entry (list stream pix))

	(vp-xor-rr col col)
	(vp-push col)
	(switch)
	(case `(,pix == 32))
		(call 'stream 'read (list stream rsp int_size) (list '_ pix))
		(gotoif `(,pix == -1) 'eof)
		(vp-pop col)
		(vp-cpy-cr 32 pix)
		(jump 'canvas 'to_argb32 (list col pix))
	(case `(,pix == 24))
		(call 'stream 'read (list stream rsp 3) (list '_ pix))
		(gotoif `(,pix == -1) 'eof)
		(vp-pop col)
		(vp-cpy-cr 24 pix)
		(jump 'canvas 'to_argb32 (list col pix))
	(case `(,pix == 16))
		(call 'stream 'read (list stream rsp short_size) (list '_ pix))
		(gotoif `(,pix == -1) 'eof)
		(vp-pop col)
		(vp-cpy-cr 16 pix)
		(jump 'canvas 'to_argb32 (list col pix))
	(default)
	(vp-label 'eof)
		(vp-pop col)
		(vp-cpy-cr -1 col)
	(endswitch)

	(exit (list col))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'to_argb32)
	;inputs
	;r0 = col (uint)
	;r1 = pixel type (uint)
	;outputs
	;r0 = col (uint)

	(vp-def (col pix rh rl gh gl bh bl) '(r0 r1))

	(entry (list col pix))

	(switch)
	(case `(,pix == 32))
		(vp-cpy-cr argb_black pix)
		(vp-xor-rr pix col)
		(break)
	(case `(,pix == 24))
		(vp-cpy-cr argb_black pix)
		(vp-add-rr pix col)
		(break)
	(default)
		(vp-cpy-rr col rh)
		(vp-cpy-rr col rl)
		(vp-cpy-rr col gh)
		(vp-cpy-rr col gl)
		(vp-cpy-rr col bh)
		(vp-cpy-rr col bl)
		(vp-and-cr 0b1111100000000000 rh)
		(vp-and-cr 0b1110000000000000 rl)
		(vp-and-cr 0b11111100000 gh)
		(vp-and-cr 0b11000000000 gl)
		(vp-and-cr 0b11111 bh)
		(vp-and-cr 0b11100 bl)
		(vp-shl-cr 8 rh)
		(vp-shl-cr 3 rl)
		(vp-shl-cr 5 gh)
		(vp-shr-cr 1 gl)
		(vp-shl-cr 3 bh)
		(vp-shr-cr 2 bl)
		(vp-cpy-cr argb_black col)
		(vp-add-rr rh rl)
		(vp-add-rr gh gl)
		(vp-add-rr bh bl)
		(vp-add-rr rl col)
		(vp-add-rr gl col)
		(vp-add-rr bl col)
	(endswitch)

	(exit (list col))
	(vp-ret)

(def-func-end)

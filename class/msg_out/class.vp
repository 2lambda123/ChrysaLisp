(include 'sys/func.inc)
(include 'sys/kernel/class.inc)
(include 'class/msg_out/class.inc)
(include 'sys/link/class.inc)

(gen-create 'msg_out)
(gen-vtable 'msg_out)

(def-method 'msg_out 'init)
	;inputs
	;r0 = msg_out object (ptr)
	;r1 = vtable (pptr)
	;r2 = target mailbox id (id)
	;outputs
	;r0 = msg_out object (ptr)
	;r1 = 0 if error, else ok
	;trashes
	;all but r0

	(entry 'msg_out 'init '(r0 r1 r2))
	(assign '(r2) `((r0 ,(+ msg_out_id id_id))))

	;init parent
	(s-call 'msg_out 'init '(r0 r1 0 0 0 0) '(r0 r1))
	(vpif '(r1 /= 0))
		;init myself
		(vp-xor-rr r2 r2)
		(assign '(r2 r2 stream_mail_state_started) '((r0 msg_out_seqnum) (r0 msg_out_ack_seqnum) r2))
		(assign '(r2) '((r0 msg_out_state)))
		(vp-push r0)
		(call 'sys_mail 'alloc_mbox nil '(r2 r1))
		(vp-pop r0)
		(assign '(r2 r1 r0) '((r0 msg_out_ack_mbox_id) (r0 msg_out_ack_mbox_addr) r1))
	(endif)

	(exit 'msg_out 'init '(r0 r1))
	(vp-ret)

(def-func-end)

(def-method 'msg_out 'deinit)
	;inputs
	;r0 = msg_out object (ptr)
	;outputs
	;r0 = msg_out object (ptr)
	;trashes
	;all but r0

	(entry 'msg_out 'deinit '(r0))

	;do we need to send stopping ?
	(assign '((r0 msg_out_state)) '(r1))
	(vpif '(r1 = stream_mail_state_started))
		;flush remaining
		(call 'msg_out 'write_flush '(r0) '(r0))

		;send stopping
		(assign '(stream_mail_state_stopping) '(r1))
		(assign '(r1) '((r0 msg_out_state)))
		(call 'msg_out 'write_next '(r0) '(r0))
		(call 'msg_out 'write_flush '(r0) '(r0))
	(endif)

	;do we need to send stopped ?
	(assign '((r0 msg_out_state)) '(r1))
	(vpif '(r1 = stream_mail_state_stopping))
		;send stopped
		(assign '(stream_mail_state_stopped) '(r1))
		(assign '(r1) '((r0 msg_out_state)))
		(call 'msg_out 'write_next '(r0) '(r0))
		(call 'msg_out 'write_flush '(r0) '(r0))
	(endif)

	;wait for all outstanding acks
	(assign '((r0 msg_out_seqnum)) '(r1))
	(vp-sub-cr 1 r1)
	(vp-shr-cr msg_out_ack_shift r1)
	(vp-add-cr 1 r1)
	(call 'msg_out 'wait_acks '(r0 r1) '(r0))

	;free any stream mail buffer
	(vp-push r0)
	(vp-xor-rr r1 r1)
	(assign '((r0 stream_buffer)) '(r2))
	(assign '(r1) '((r0 stream_buffer)))
	(call 'sys_mail 'free '(r2))

	;free ack mailbox
	(assign '((rsp 0)) '(r0))
	(call 'sys_mail 'free_mbox '((r0 msg_out_ack_mbox_id)))

	;deinit parent
	(vp-pop r0)
	(s-jump 'msg_out 'deinit '(r0))

(def-func-end)

(def-method 'msg_out 'wait_acks)
	;inputs
	;r0 = msg_out object (ptr)
	;r1 = msg ack num (uint)
	;outputs
	;r0 = msg_out object (ptr)
	;trashes
	;all but r0

	(entry 'msg_out 'wait_acks '(r0 r1))

	(loop-start)
		(assign '((r0 msg_out_ack_seqnum)) '(r2))
		(breakif '(r1 = r2))
		;next ack seq num
		(vp-add-cr 1 r2)
		(assign '(r2) '((r0 msg_out_ack_seqnum)))
		;wait for an ack
		(vp-push r0 r1)
		(call 'sys_mail 'read '((r0 msg_out_ack_mbox_addr)) '(r0 _))
		(call 'sys_mail 'free '(r0))
		(vp-pop r0 r1)
	(loop-end)

	(exit 'msg_out 'wait_acks '(r0))
	(vp-ret)

(def-func-end)

(def-method 'msg_out 'write_flush)
	;inputs
	;r0 = msg_out object (ptr)
	;outputs
	;r0 = msg_out object (ptr)
	;trashes
	;all but r0

	(entry 'msg_out 'write_flush '(r0))

	(assign '((r0 stream_buffer)) '(r1))
	(vpif '(r1 /= 0))
		(vp-push r0)
		(vp-xor-rr r2 r2)
		(assign '(r2) '((r0 stream_buffer)))

		;send current buffer
		(assign '((r1 msg_parcel_data) (r0 stream_bufp)) '(r2 r3))
		(vp-sub-rr r2 r3)
		(assign `(r3 (r0 ,(+ msg_out_id id_id)) (r0 msg_out_seqnum) (r0 msg_out_state)
			(r0 msg_out_ack_mbox_id)) '((r1 msg_length) r3 r4 r5 r6))
		(assign '(r3 r4 r5 r6) `((r1 ,(+ msg_dest id_id))(r2 stream_msg_seqnum)
			(r2 stream_msg_state) (r2 ,(+ stream_msg_ack_id id_mbox) ui)))

		(call 'sys_kernel 'id nil '((r2 (+ stream_msg_ack_id id_cpu) i)))
		(call 'sys_mail 'send '(r1))

		;wait for acks ?
		(vp-pop r0)
		(assign '((r0 msg_out_seqnum)) '(r1))
		(vp-shr-cr msg_out_ack_shift r1)
		(call 'msg_out 'wait_acks '(r0 r1) '(r0))

		;next seq num
		(assign '((r0 msg_out_seqnum)) '(r1))
		(vp-add-cr 1 r1)
		(assign '(r1) '((r0 msg_out_seqnum)))

		;parent actions
		(s-call 'msg_out 'write_flush '(r0))
	(endif)

	(exit 'msg_out 'write_flush '(r0))
	(vp-ret)

(def-func-end)

(def-method 'msg_out 'write_next)
	;inputs
	;r0 = msg_out object (ptr)
	;outputs
	;r0 = msg_out object (ptr)
	;trashes
	;all but r0

	(entry 'msg_out 'write_next '(r0))

	(vp-push r0)
	(call 'stream 'write_flush '(r0))
	(call 'sys_mail 'alloc '(lk_data_size) '(r2 r1))
	(vp-pop r0)
	(vp-lea-i r1 lk_data_size r3)
	(vp-add-cr stream_msg_data r1)
	(assign '(r2 r1 r3) '((r0 stream_buffer) (r0 stream_bufp) (r0 stream_bufe)))

	(exit 'msg_out 'write_next '(r0))
	(vp-ret)

(def-func-end)

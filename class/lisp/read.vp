(include 'sys/func.inc)
(include 'class/stream/class.inc)
(include 'class/lisp/class.inc)
(include 'class/str/class.inc)

(def-method 'lisp 'read)
	;inputs
	;r0 = lisp object (ptr)
	;r1 = stream object (ptr)
	;r2 = next char (uint)
	;outputs
	;r0 = lisp object (ptr)
	;r1 = ast vector (ptr)
	;r2 = next char (uint)

	(ptr 'this 'stream 'ast)
	(int 'char)

	(push-scope)
	(entry 'lisp 'read {this, stream, char})

	;skip white space
(vp-label 'skip_white)
	(loop-whilenot {char > char_space || char = -1})
		(call 'lisp 'read_char {this, stream, char} {_, char})
	(loop-end)

	;what are we reading ?
	(vpif {char /= -1})
		(switch)
		(vpcase {char = char_semi})
			(call 'stream 'skip_not {stream, char_lf})
			(call 'stream 'read_char {stream} {_, char})
			(goto 'skip_white)
		(vpcase {char = char_rrb})
			(call 'lisp 'repl_error {this, "unexpected )", 0, this->lisp_sym_nil} {_, ast})
			(call 'stream 'read_char {stream} {_, char})
			(break)
		(vpcase {char = char_rcb})
			(call 'lisp 'repl_error (cat {this, "unexpected } "}" {", 0, this->lisp_sym_nil}) {_, ast})
			(call 'stream 'read_char {stream} {_, char})
			(break)
		(vpcase {char = char_lrb})
			(call 'lisp 'read_list {this, stream, char} {_, ast, char})
			(break)
		(vpcase {char = char_minus || (char >= char_0 && char <= char_9)})
			(call 'lisp 'read_num {this, stream, char} {_, ast, char})
			(break)
		(vpcase {char = char_double_quote})
			(call 'lisp 'read_str {this, stream, char, char_double_quote} {_, ast, char})
			(break)
		(vpcase {char = char_lcb})
			(call 'lisp 'read_str {this, stream, char, char_rcb} {_, ast, char})
			(break)
		(vpcase {char = char_quote})
			(call 'lisp 'read_rmacro {this, stream, char, this->lisp_sym_quote} {_, ast, char})
			(break)
		(vpcase {char = char_tick})
			(call 'lisp 'read_rmacro {this, stream, char, this->lisp_sym_qquote} {_, ast, char})
			(break)
		(vpcase {char = char_comma})
			(call 'lisp 'read_rmacro {this, stream, char, this->lisp_sym_unquote} {_, ast, char})
			(break)
		(vpcase {char = char_tilda})
			(call 'lisp 'read_rmacro {this, stream, char, this->lisp_sym_splicing} {_, ast, char})
			(break)
		(default)
			(call 'lisp 'read_sym {this, stream, char} {_, ast, char})
		(endswitch)
	(else)
		(call 'obj 'ref {this->lisp_sym_nil} {ast})
	(endif)

	(exit 'lisp 'read {this, ast, char})
	(pop-scope)
	(return)

(def-func-end)

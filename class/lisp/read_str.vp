(include 'sys/func.inc)
(include 'sys/load/class.inc)
(include 'class/str/class.inc)
(include 'class/stream_str/class.inc)
(include 'class/lisp/class.inc)

(def-method 'lisp :read_str)
	;inputs
	;r0 = lisp object (ptr)
	;r1 = stream object (ptr)
	;r2 = close char (uint)
	;outputs
	;r0 = lisp object (ptr)
	;r1 = str object (ptr)
	;r2 = next char (uint)
	;trashes
	;r1-r14

	(ptr 'this 'stream)
	(union
		(ptr 'str_stream 'value)
		(pubyte 'bufp 'bufe))
	(int 'char 'cchar)

	(push-scope)
	(entry 'lisp :read_str {this, stream, cchar})

	(switch)
		(assign {stream->stream_bufp, stream->stream_bufe} {bufp, bufe})
		(loop-start)
			(assign {*bufp} {char})
			(gotoif {bufp = bufe || char = char_lf} 'slow_but_sure)
			(assign {bufp + byte_size} {bufp})
		(loop-until {char = cchar})
		(call 'str :create_from_buffer {stream->stream_bufp, bufp - stream->stream_bufp - byte_size} {value})
		(assign {bufp} {stream->stream_bufp})
	(else)
	(vp-label 'slow_but_sure)
		(call 'str :create_from_buffer {0, str_gap} {value})
		(call 'stream_str :create {value} {str_stream})
		(call 'stream :read_char {stream} {_, char})
		(loop-whilenot {char = -1 || char = cchar})
			(call 'stream :write_char {str_stream, char})
			(call 'lisp :read_char {this, stream, char} {_, char})
		(loop-end)
		(call 'stream_str :ref_string {str_stream} {_, value})
		(call 'stream_str :destroy {str_stream})
	(endswitch)
	(call 'stream :read_char {stream} {_, char})

	(exit 'lisp :read_str {this, value, char})
	(pop-scope)
	(return)

(def-func-end)

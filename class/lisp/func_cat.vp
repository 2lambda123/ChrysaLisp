(include 'sys/func.inc)
(include 'class/string/class.inc)
(include 'gui/points/class.inc)
(include 'class/lisp/class.inc)

(def-method 'lisp 'func_cat)
	;inputs
	;r0 = lisp object (ptr)
	;r1 = args vector object (ptr)
	;outputs
	;r0 = lisp object (ptr)
	;r1 = return value object (ptr)

	(vp-def (r_this r_value r_iter_begin r_iter_end r_args r_length r_elem)
		'(r10 r11 r12 r13 r6 r7))

	(entry 'lisp 'func_cat (list r_this r_args))

	(d-call 'vector 'get_length (list r_args) (list r_args r_length))
	(errorif (list r_length '== 0) 'error)

	(call 'vector 'get_iters (list r_args) (list '_ r_iter_begin r_iter_end))
	(call 'lisp 'env_args_type (list r_args `(@ ,(f-path 'string 'vtable)) -1))
	(vpif (list (method-output 'lisp 'env_args_type 0) '!= 0))
		(vpif (list r_length '== 2))
			(call 'string 'append `((,r_iter_begin 0) (,r_iter_begin ptr_size)) (list r_value))
		(else)
			(call 'string 'cat (list r_args) (list r_value))
		(endif)
	(else)
		(call 'lisp 'env_args_type (list r_args `(@ ,(f-path 'vector 'vtable)) -1))
		(vpif (list (method-output 'lisp 'env_args_type 0) '!= 0))
			(call 'vector 'create nil (list r_value))
		(vp-label 'docat)
			(loop-start)
				(call 'array 'get_length `((,r_iter_begin 0)) (list r_elem r_length))
				(call 'array 'append (list r_value r_elem 0 r_length) (list r_value))
				(vp-add-cr ptr_size r_iter_begin)
			(loop-until (list r_iter_begin '== r_iter_end))
		(else)
			(call 'lisp 'env_args_type (list r_args `(@ ,(f-path 'array 'vtable)) -1))
			(vpif (list (method-output 'lisp 'env_args_type 0) '!= 0))
				(call 'array 'create nil (list r_value))
				(goto 'docat)
			(else)
			(errorcases
				(call 'lisp 'env_args_type (list r_args `(@ ,(f-path 'points 'vtable)) -1))
				(errorif (list (method-output 'lisp 'env_args_type 0) '== 0) 'error))
				(call 'points 'create nil (list r_value))
				(goto 'docat)
			(endif)
		(endif)
	(endif)

	(exit 'lisp 'func_cat (list r_this r_value))
	(vp-ret)

(errorcases
(vp-label 'error)
	(jump 'lisp 'repl_error (list r_this "(cat seq ...)" error_msg_wrong_types r_args)))

(def-func-end)

(include 'sys/func.inc)
(include 'class/stream/class.inc)
(include 'class/sym/class.inc)
(include 'class/func/class.inc)
(include 'class/pair/class.inc)
(include 'class/hset/class.inc)
(include 'class/hmap/class.inc)
(include 'gui/points/class.inc)
(include 'class/lisp/class.inc)

(def-method 'lisp 'repl_print)
	;inputs
	;r0 = lisp object (ptr)
	;r1 = stream object (ptr)
	;r2 = value
	;outputs
	;r0 = lisp object (ptr)

	(def-struct 'local)
		(ptr 'this 'stream)
		(pptr 'iter_end)
		(uint 'length)
	(def-struct-end)

	(ptr 'this 'stream)
	(pptr 'iter_end)
	(uint 'length)

	(ptr 'value 'elem)

	(push-scope)
	(entry 'lisp 'repl_print {this, stream, value})

	(vpifnot {value})
		(call 'stream 'write_char {stream, char_lf})
		(call 'stream 'write_cstr {stream, "*NULL*"})
		(call 'stream 'write_char {stream, char_lf})
	(else)
		(assign {value->obj_vtable} {elem})
		(switch)
		(vpcase (cat {elem == @} (f-path 'sym 'vtable)))
			(call 'stream 'write {stream, &value->str_data, value->str_length})
			(break)
		(vpcase (cat {elem == @} (f-path 'str 'vtable)))
			(call 'stream 'write_char {stream, char_double_quote})
			(call 'stream 'write {stream, &value->str_data, value->str_length})
			(call 'stream 'write_char {stream, char_double_quote})
			(break)
		(vpcase (cat {elem == @} (f-path 'num 'vtable)))
			(call 'num 'get_value {value} {_, elem})
			(call 'str 'create_from_long {elem, 10} {value})
			(call 'stream 'write {stream, &value->str_data, value->str_length})
			(call 'obj 'deref {value})
			(break)
		(vpcase (cat {elem == @} (f-path 'func 'vtable)))
			(call 'stream 'write_cstr {stream, "#0x"})
			(call 'func 'get_value {value} {_, elem})
			(call 'str 'create_from_long {elem, 16} {value})
			(call 'stream 'write {stream, &value->str_data, value->str_length})
			(call 'obj 'deref {value})
			(break)
		(vpcase (cat {elem == @} (f-path 'pair 'vtable)))
			(call 'stream 'write_char {stream, char_lsb})
			(call 'pair 'get_first {value} {_, elem})
			(call 'lisp 'repl_print {this, stream, elem})
			(call 'stream 'write_char {stream, char_space})
			(call 'pair 'get_second {value} {_, elem})
			(call 'lisp 'repl_print {this, stream, elem})
			(call 'stream 'write_char {stream, char_rsb})
			(break)
		(vpcase (cat {elem == @} (f-path 'hset 'vtable)))
			(call 'stream 'write_char {stream, char_lsb})
			(call 'hset 'get_iters {value} {_, _, iter_end})
			(assign {iter_end - ptr_size} {iter_end})
			(call 'hset 'for_each {value, $callback, &this})
			(call 'stream 'write_char {stream, char_rsb})
			(break)
		(vpcase (cat {elem == @} (f-path 'hmap 'vtable)))
			(call 'stream 'write_char {stream, char_lcb})
			(call 'hmap 'get_iters {value} {_, _, iter_end})
			(assign {iter_end - ptr_size} {iter_end})
			(call 'hmap 'for_each {value, $callback, &this})
			(call 'stream 'write_char {stream, char_rcb})
			(break)
		(vpcase (cat {elem == @} (f-path 'array 'vtable) { || elem == @} (f-path 'points 'vtable)))
			(call 'stream 'write_char {stream, char_lsb})
			(d-call 'array 'get_length {value} {_, length})
			(call 'array 'get_end {value} {_, iter_end})
			(assign {iter_end - long_size} {iter_end})
			(call 'array 'for_each {value, 0, length, $callback1, &this})
			(call 'stream 'write_char {stream, char_rsb})
			(break)
		(vpcase (cat {elem == @} (f-path 'error 'vtable)))
			(call 'stream 'write_cstr {stream, "Error: "})
			(call 'error 'get_description {value} {_, elem})
			(call 'stream 'write {stream, &elem->str_data, elem->str_length})
			(call 'stream 'write_cstr {stream, " "})
			(call 'error 'get_msg {value} {_, elem})
			(call 'stream 'write_cstr {stream, elem})
			(call 'stream 'write_cstr {stream, " < "})
			(call 'error 'get_object {value} {_, elem})
			(call 'lisp 'repl_print {this, stream, elem})
			(call 'stream 'write_cstr {stream, " > File: "})
			(call 'error 'get_file {value} {_, elem})
			(call 'stream 'write {stream, &elem->str_data, elem->str_length})
			(call 'stream 'write_cstr {stream, "("})
			(call 'error 'get_line {value} {_, elem})
			(call 'str 'create_from_long {elem, 10} {elem})
			(call 'stream 'write {stream, &elem->str_data, elem->str_length})
			(call 'obj 'deref {elem})
			(call 'stream 'write_cstr {stream, ")"})
			(break)
		(vpcase (cat {elem == @} (f-path 'vector 'vtable)))
			(d-call 'vector 'get_length {value} {_, length})
			(vpif {length})
				(call 'vector 'get_first {value} {_, elem})
				(switch)
				(vpcase {elem == this->lisp_sym_quote})
					(call 'stream 'write_char {stream, char_quote})
					(break)
				(vpcase {elem == this->lisp_sym_qquote})
					(call 'stream 'write_char {stream, char_tick})
					(break)
				(vpcase {elem == this->lisp_sym_unquote})
					(call 'stream 'write_char {stream, char_comma})
					(break)
				(vpcase {elem == this->lisp_sym_splicing})
					(call 'stream 'write_char {stream, char_tilda})
					(break)
				(default)
					(goto 'notquote)
				(endswitch)
				(vpif {length > 1})
					(call 'vector 'get_second {value} {_, elem})
					(call 'lisp 'repl_print {this, stream, elem})
				(endif)
			(else)
			(vp-label 'notquote)
				(call 'stream 'write_char {stream, char_lrb})
				(call 'vector 'get_end {value} {_, iter_end})
				(assign {iter_end - ptr_size} {iter_end})
				(call 'vector 'for_each {value, 0, length, $callback, &this})
				(call 'stream 'write_char {stream, char_rrb})
			(endif)
			(break)
		(default)
			(pubyte 'name_offset)
			(push-scope)
			(assign {elem - 1} {name_offset})
			(assign {elem - *name_offset} {elem})
			(call 'stream 'write_char {stream, char_at})
			(call 'stream 'write_cstr {stream, elem})
			(pop-scope)
		(endswitch)
	(endif)

	(exit 'lisp 'repl_print {this})
	(pop-scope)
	(return)

(vp-label 'callback)
	;inputs
	;r0 = predicate data (ptr)
	;r1 = element iterator (ptr)
	;outputs
	;r1 = 0 if break, else not

	(ptr 'pdata)
	(pptr 'iter)

	(push-scope)
	(entry 'vector 'each_callback {pdata, iter})

	(call 'lisp 'repl_print {pdata->local_this, pdata->local_stream, *iter})
	(vpif {pdata->local_iter_end != iter})
		(call 'stream 'write_char {pdata->local_stream, char_space})
	(endif)

	(exit 'vector 'each_callback '(-1))
	(pop-scope)
	(return)

(vp-label 'callback1)
	;inputs
	;r0 = predicate data (ptr)
	;r1 = element iterator (ptr)
	;outputs
	;r1 = 0 if break, else not

	(ptr 'pdata 'value)
	(plong 'iter)

	(push-scope)
	(entry 'array 'each_callback {pdata, iter})

	(vpif {*iter < 0})
		(call 'str 'create_from_long {-(*iter), 16} {value})
		(call 'stream 'write_cstr {pdata->local_stream, "-"})
	(else)
		(call 'str 'create_from_long {*iter, 16} {value})
	(endif)
	(call 'stream 'write_cstr {pdata->local_stream, "0x"})
	(call 'stream 'write {pdata->local_stream, &value->str_data, value->str_length})
	(call 'obj 'deref {value})
	(vpif {pdata->local_iter_end != iter})
		(call 'stream 'write_char {pdata->local_stream, char_space})
	(endif)

	(exit 'array 'each_callback '(-1))
	(pop-scope)
	(return)

(def-func-end)

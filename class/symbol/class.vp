(include 'sys/func.inc)
(include 'class/symbol/class.inc)
(include 'class/hash_set/class.inc)
(include 'class/vector/class.inc)

(gen-vtable 'symbol)

(def-method 'symbol 'statics)
	;info
	;symbol static data
	(times symbol_statics_size (vp-byte 0))
(def-func-end)

(def-method 'symbol 'intern)
	;inputs
	;r0 = symbol object (ptr)
	;outputs
	;r0 = interned symbol object (ptr)
	;trashes
	;all

	(def-struct 'local)
		(ptr 'this)
		(pptr 'iter)
	(def-struct-end)

	(vp-alloc local_size)
	(entry 'symbol 'intern '((rsp local_this)))

	(fn-bind (f-path 'symbol 'statics) r0)
	(vp-cpy-ir r0 symbol_statics_intern r0)
	(vpif '(r0 == 0))
		(call 'hash_set 'create `((@ ,(f-path 'symbol 'same)) 101) '(r0))
		(fn-bind (f-path 'symbol 'statics) r1)
		(vp-cpy-ri r0 r1 symbol_statics_intern)
	(endif)
	(call 'hash_set 'insert '(r0 (rsp local_this)) '(_ (rsp local_iter) _))
	(call 'obj 'deref '((rsp local_this)))
	(vp-cpy-ir rsp local_iter r0)
	(call 'obj 'ref '((r0 0)) '(r0))
	(assign `((@ ,(f-path 'symbol 'vtable))) '(r1))
	(vp-cpy-ri r1 r0 obj_vtable)

	(exit 'symbol 'intern '(r0))
	(vp-free local_size)
	(vp-ret)

(def-func-end)

(def-method 'symbol 'intern_cstr)
	;inputs
	;r0 = c string (ptr)
	;outputs
	;r0 = interned symbol object (ptr)
	;trashes
	;all

	(entry 'symbol 'intern_cstr '(r0))
	(call 'symbol 'create_from_cstr '(r0) '(r0))
	(jump 'symbol 'intern '(r0))

(def-func-end)

(def-method 'symbol 'flush)
	;trashes
	;all

	(def-struct 'local)
		(ptr 'new_set)
	(def-struct-end)

	(vp-alloc local_size)

	(fn-bind (f-path 'symbol 'statics) r0)
	(vp-cpy-ir r0 symbol_statics_intern r0)
	(call 'hash_set 'create '((r0 hash_set_key_callback) (r0 hash_set_num_buckets ui)) '(r0))
	(vp-cpy-ri r0 rsp local_new_set)
	(fn-bind (f-path 'symbol 'statics) r0)
	(vp-cpy-ir r0 symbol_statics_intern r0)
	(call 'hash_set 'for_each '(r0 ($ flush_callback) (& rsp local_new_set)) '(r0 _ _))
	(call 'hash_set 'deref '(r0))
	(vp-cpy-ir rsp local_new_set r0)
	(fn-bind (f-path 'symbol 'statics) r1)
	(vp-cpy-ri r0 r1 symbol_statics_intern)

	(vp-free local_size)
	(vp-ret)

(vp-label 'flush_callback)
	;inputs
	;r0 = predicate data (ptr)
	;r1 = element iterator (pptr)
	;outputs
	;r1 = 0 if break, else not

	(vp-cpy-ir r1 0 r1)
	(vp-cpy-ir-ui r1 obj_count r2)
	(vpif '(r2 != 1))
		(vp-cpy-ir r0 0 r2)
		(call 'obj 'ref '(r1) '(r0))
		(vp-push r0)
		(call 'hash_set 'get_bucket '(r2 r0) '(_ r0))
		(vp-pop r1)
		(jump 'vector 'push_back '(r0 r1))
	(endif)
	(vp-ret)

(def-func-end)

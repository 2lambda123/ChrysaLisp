;;;;;;
; math
;;;;;;

(import "lib/math/vector.inc")

;useful real constants
(defmacro def_real (c s e f)
	`(bind (map (# (sym (str ,c %0))) (range ,s ,e)) (map ,f (range ,s ,e))))
;[0:10]
(def_real "+real_" 0 11 (# (i2r %0)))
;[-1:-10]
(def_real "+real_" -10 0 (# (i2r %0)))
;[1/2:1/20]
(def_real "+real_1/" 1 21 (# (/ (const (i2r 1)) (i2r %0))))
;pi and temp vec4
(defq +real_pi (f2r +fp_pi) +real_2pi (f2r +fp_2pi)
	+reals_tmp (reals +real_0 +real_0 +real_0 +real_0))

(enums +vertex 0
	(enum x y z w))

(enums +matrix 0
	(enum r0 r1 r2 r3))

(defun vertex-r (x y z)
	(reals x y z +real_1))

(defun vertex-f (x y z)
	(reals (f2r x) (f2r y) (f2r z) +real_1))

(defun matrix-unity ()
	(list (reals +real_1 +real_0 +real_0 +real_0)
		(reals +real_0 +real_1 +real_0 +real_0)
		(reals +real_0 +real_0 +real_1 +real_0)
		(reals +real_0 +real_0 +real_0 +real_1)))

(defun matrix-rotx (a)
	(list (reals +real_1 +real_0 +real_0 +real_0)
		(reals +real_0 (cos a) (* (sin a) +real_-1) +real_0)
		(reals +real_0 (sin a) (cos a) +real_0)
		(reals +real_0 +real_0 +real_0 +real_1)))

(defun matrix-roty (a)
	(list (reals (cos a) +real_0 (sin a) +real_0)
		(reals +real_0 +real_1 +real_0 +real_0)
		(reals (* (sin a) +real_-1) +real_0 (cos a) +real_0)
		(reals +real_0 +real_0 +real_0 +real_1)))

(defun matrix-rotz (a)
	(list (reals (cos a) (* (sin a) +real_-1) +real_0 +real_0)
		(reals (sin a) (cos a) +real_0 +real_0)
		(reals +real_0 +real_0 +real_1 +real_0)
		(reals +real_0 +real_0 +real_0 +real_1)))

(defun matrix-translate (x y z)
	(list (reals +real_1 +real_0 +real_0 x)
		(reals +real_0 +real_1 +real_0 y)
		(reals +real_0 +real_0 +real_1 z)
		(reals +real_0 +real_0 +real_0 +real_1)))

(defun matrix-frustum (left right top bottom near far)
	(list (reals (/ (* +real_2 near) (- right left)) +real_0 (/ (+ left right) (- right left)) +real_0)
		(reals +real_0 (/ (* +real_2 near) (- top bottom)) (/ (+ top bottom) (- top bottom)) +real_0)
		(reals +real_0 +real_0 (/ (+ near far) (- near far)) (/ (* +real_2 near far) (- near far)))
		(reals +real_0 +real_0 +real_-1 +real_0)))

(defun vertex-mul (m v)
	(reals (vec-dot (elem +matrix_r0 m) v +reals_tmp)
		(vec-dot (elem +matrix_r1 m) v +reals_tmp)
		(vec-dot (elem +matrix_r2 m) v +reals_tmp)
		(vec-dot (elem +matrix_r3 m) v +reals_tmp)))

(defun matrix-mul (ma mb)
	(defq mar0 (elem +matrix_r0 ma) mar1 (elem +matrix_r1 ma)
		mar2 (elem +matrix_r2 ma) mar3 (elem +matrix_r3 ma)
		mbr0 (elem +matrix_r0 mb) mbr1 (elem +matrix_r1 mb)
		mbr2 (elem +matrix_r2 mb) mbr3 (elem +matrix_r3 mb)
		mbc0 (reals (elem 0 mbr0) (elem 0 mbr1) (elem 0 mbr2) (elem 0 mbr3))
		mbc1 (reals (elem 1 mbr0) (elem 1 mbr1) (elem 1 mbr2) (elem 1 mbr3))
		mbc2 (reals (elem 2 mbr0) (elem 2 mbr1) (elem 2 mbr2) (elem 2 mbr3))
		mbc3 (reals (elem 3 mbr0) (elem 3 mbr1) (elem 3 mbr2) (elem 3 mbr3)))
	(list (reals (vec-dot mar0 mbc0 +reals_tmp)
			 (vec-dot mar0 mbc1 +reals_tmp)
			 (vec-dot mar0 mbc2 +reals_tmp)
			 (vec-dot mar0 mbc3 +reals_tmp))
		(reals (vec-dot mar1 mbc0 +reals_tmp)
			 (vec-dot mar1 mbc1 +reals_tmp)
			 (vec-dot mar1 mbc2 +reals_tmp)
			 (vec-dot mar1 mbc3 +reals_tmp))
		(reals (vec-dot mar2 mbc0 +reals_tmp)
			 (vec-dot mar2 mbc1 +reals_tmp)
			 (vec-dot mar2 mbc2 +reals_tmp)
			 (vec-dot mar2 mbc3 +reals_tmp))
		(reals (vec-dot mar3 mbc0 +reals_tmp)
			 (vec-dot mar3 mbc1 +reals_tmp)
			 (vec-dot mar3 mbc2 +reals_tmp)
			 (vec-dot mar3 mbc3 +reals_tmp))))
